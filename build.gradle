buildscript {
    ext.kotlin_version = '1.9.23'
    ext.jackson_version = '2.16.1'
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version"
    id "java-library"
    id "maven-publish"
    id "signing"
}

group 'io.github.mslenc'
version '0.32.4'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}


repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://mslenc-releases.s3.amazonaws.com/maven2" }
}

dependencies {
    api 'io.github.mslenc:asyncdb:1.6.2'
    implementation 'io.github.mslenc:utilsktx:1.5.3'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.8.0"

    implementation "org.slf4j:slf4j-api:1.7.36"

    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    implementation "com.fasterxml.jackson.module:jackson-module-parameter-names:$jackson_version"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jackson_version"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'ch.qos.logback:logback-classic:1.3.14'
    testImplementation 'com.mysql:mysql-connector-j:8.3.0'
    testImplementation 'io.github.mslenc:gqlktx:0.30.2'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.verbose = true
}

//test.dependsOn startEmbeddedMysql
//test.finalizedBy stopEmbeddedMysql

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'dbktx'
            from components.java

            pom {
                name = "dbktx"
                description = "A database access layer with auto-batching, based on asyncdb."
                url = "https://github.com/mslenc/dbktx"

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'mslenc'
                        name = 'Mitja Slenc'
                        email = 'mslenc@gmail.com'
                    }
                }

                scm {
                    connection = "scm:git:https://github.com/mslenc/dbktx.git"
                    developerConnection = "scm:git:git@github.com:mslenc/dbktx.git"
                    url = "https://github.com/mslenc/dbktx"
                }
            }
        }
    }

    repositories {
        maven {
            url = "s3://mslenc-releases/maven2"
            credentials(AwsCredentials) {
                accessKey = awsAccessKeyMslenc
                secretKey = awsSecretKeyMslenc
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

